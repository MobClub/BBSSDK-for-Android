// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
        maven {
            url "http://mvn.mob.com/android"
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.1'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
        classpath 'com.mob.sdk:MobSDK:+'
    }
}

allprojects {
    repositories {
        jcenter()
        flatDir {
            dirs 'libs',  project(':BBSSDKGUI').getProjectDir().getPath() + '/libs'
        }
    }
}

ext{
    compileSdkVersion = 23
    buildToolsVersion = findBuildTools()

    minSdkVersion = 19
    targetSdkVersion = 23
    versionCode = 6
    versionName = "2.2.0"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

String findBuildTools() {
    def sdkDir = System.env.ANDROID_HOME;
    if(sdkDir == null) {
        def localProperties = new File(project.rootDir, "local.properties")
        if (localProperties.exists()) {
            Properties properties = new Properties()
            localProperties.withInputStream {
                instr->properties.load(instr)
            }
            sdkDir = properties.getProperty('sdk.dir')
        }
    }
    if (sdkDir != null) {
        def buildTools = new File(sdkDir, "build-tools")
        if (buildTools.exists()) {
            def tools = buildTools.list()
            if (tools != null) {
                Arrays.sort(tools)
                return tools[tools.length - 1]
            }
        }
    }
    return null
}